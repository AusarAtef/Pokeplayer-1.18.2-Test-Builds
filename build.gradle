buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
        flatDir {
            dirs 'libs'
        }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id 'org.ajoberstar.grgit' version '2.3.0'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'

ext {
    VERSION_COMPILE = "0.5.0" // Taken from Pokeplayer-1.18.2-Test-Builds
    VERSION_THUTCORE = "99.3.24" // Taken from Pokecube-Issues-and-Wiki
    // Force a refresh of the toml file
    cleanTOML()
}

version = "${version_minecraft}-${VERSION_COMPILE}"
group = 'pokeplayer'
archivesBaseName = "Pokecube Synchro Machine"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: "${channel_mappings}", version: "${version_mappings}"
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client = {
            taskName 'run_client'
            properties 'forge.logging.console.level': 'debug'
            properties '-XX': '+UnlockCommercialFeatures'
            properties '-XX': '+FlightRecorder'
            args "-mixin.config=pokecube.mixins.json", "-mixin.config=thutcore.mixins.json"
            workingDirectory project.file('run').canonicalPath
            source sourceSets.main
        }
        server = {
            taskName 'run_server'
            properties 'forge.logging.console.level': 'debug'
            properties '-XX': '+UnlockCommercialFeatures'
            properties '-XX': '+FlightRecorder'
            jvmArg "-Xmx2G"
            workingDirectory project.file('run').canonicalPath
            args "-mixin.config=pokecube.mixins.json", "-mixin.config=thutcore.mixins.json"
            source sourceSets.main
        }
    }
}

repositories {
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    flatDir {
        dirs 'libs'
    }
    maven {
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        name = "ModMaven"
        url = "https://modmaven.k-4u.nl"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${version_minecraft}-${version_forge}"
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    // Pokecube dependencies
    implementation fg.deobf("curse.maven:pokecube-aoi-${version_pokecube}")
    implementation fg.deobf("curse.maven:cyanide-${version_cyanide}")
    compileOnly fg.deobf("mezz.jei:jei-${version_jei_mc}:${version_jei}:api")
    runtimeOnly fg.deobf("mezz.jei:jei-${version_jei_mc}:${version_jei}")
}

sourceSets {
    main.java.srcDirs += 'src/thutcore/java'
    main.resources.srcDirs += 'src/thutcore/resources'
    main.resources.srcDirs += 'src/generated/resources'
}

jar {
    manifest {
        attributes 'FMLAT': 'accesstransformer.cfg',
        "Specification-Title": "${compile_name}",
        "Specification-Vendor": "${author_name}",
        "Specification-Version": VERSION_COMPILE,
        "Implementation-Title": project.name,
        "Implementation-Version": VERSION_COMPILE,
        "Implementation-Vendor": "${author_name}",
        "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    }
}

processResources.doLast {
    def TOMLFILE = "./build/resources/main/META-INF/mods.toml"
    replaceParameters(TOMLFILE, "version_thutcore", VERSION_THUTCORE)
    replaceParameters(TOMLFILE, "version_mc", "${version_mcsupport}")
    replaceParameters(TOMLFILE, "version_forge", "${version_forge}")
}

def replaceParameters(filename, find, replace) {
    ant.replace(file: filename, token: find, value: replace)
}

def cleanTOML() {
    delete "./build/resources/main/META-INF/mods.toml"
}